//! zinc

  library GlobalEvents requires CothUtilities {

    timer tempTimer[8000];
    trigger tempTrigger[8000];
    integer tempInteger[8000];
    unit tempUnit[8000];

    hashtable data = InitHashtable();

    function eventDamagedHandler()  -> boolean {
      unit damager = GetEventDamageSource();

      //Desperation handling
      if (GetUnitAbilityLevel(damager, desperation) > 0 && IsUnitEnemy(GetTriggerUnit(), GetOwningPlayer(damager))) {
        if (GetUnitAbilityLevel(damager, desperationBuffAttackSpeed) < 17) {
          tempTimer[0] = CreateTimer();
          SaveUnitHandle(data, GetHandleId(tempTimer[0]), StringHash("GalenUnit"), damager);
          if (GetUnitAbilityLevel(damager, desperationBuffAttackSpeed) + GetUnitAbilityLevel(damager, desperation) > 17) {
            SaveInteger(data, GetHandleId(tempTimer[0]), StringHash("AttackSpeedBuffLevel"), 17 - GetUnitAbilityLevel(damager, desperationBuffAttackSpeed));
            SetUnitAbilityLevel(damager, desperationBuffAttackSpeed, 17);
          } else {
            SaveInteger(data, GetHandleId(tempTimer[0]), StringHash("AttackSpeedBuffLevel"), GetUnitAbilityLevel(damager, desperation));
            SetUnitAbilityLevel(damager, desperationBuffAttackSpeed, GetUnitAbilityLevel(damager, desperationBuffAttackSpeed) + GetUnitAbilityLevel(damager, desperation));
          }
          
          TimerStart(tempTimer[0], 3.5, false, function(){
            SetUnitAbilityLevel(LoadUnitHandle(data, GetHandleId(GetExpiredTimer()), StringHash("GalenUnit")), desperationBuffAttackSpeed, 
            GetUnitAbilityLevel(LoadUnitHandle(data, GetHandleId(GetExpiredTimer()), StringHash("GalenUnit")), desperationBuffAttackSpeed) - 
            LoadInteger(data, GetHandleId(GetExpiredTimer()), StringHash("AttackSpeedBuffLevel")));
            UnitAddAbility(LoadUnitHandle(data, GetHandleId(GetExpiredTimer()), StringHash("GalenUnit")), 'B04W');
            UnitAddAbility(LoadUnitHandle(data, GetHandleId(GetExpiredTimer()), StringHash("GalenUnit")), 'B04V');
            FlushChildHashtable(data, GetHandleId(GetExpiredTimer()));
            DestroyTimer(GetExpiredTimer());
          });
        }
        if (GetUnitAbilityLevel(damager, desperationBuffMovementSpeed) < 5) {
          tempTimer[0] = CreateTimer();
          SaveUnitHandle(data, GetHandleId(tempTimer[0]), StringHash("GalenUnit"), damager);
          if (GetUnitAbilityLevel(damager, desperationBuffMovementSpeed) + GetUnitAbilityLevel(damager, desperation) > 5) {
            SaveInteger(data, GetHandleId(tempTimer[0]), StringHash("MovementSpeedBuffLevel"), 5 - GetUnitAbilityLevel(damager, desperationBuffMovementSpeed));
            SetUnitAbilityLevel(damager, desperationBuffMovementSpeed, 5);
          } else {
            SaveInteger(data, GetHandleId(tempTimer[0]), StringHash("MovementSpeedBuffLevel"), GetUnitAbilityLevel(damager, desperation));
            SetUnitAbilityLevel(damager, desperationBuffMovementSpeed, GetUnitAbilityLevel(damager, desperationBuffMovementSpeed) + GetUnitAbilityLevel(damager, desperation));
          }
          TimerStart(tempTimer[0], 3.5, false, function(){
            SetUnitAbilityLevel(LoadUnitHandle(data, GetHandleId(GetExpiredTimer()), StringHash("GalenUnit")), desperationBuffMovementSpeed, 
            GetUnitAbilityLevel(LoadUnitHandle(data, GetHandleId(GetExpiredTimer()), StringHash("GalenUnit")), desperationBuffMovementSpeed) - 
            LoadInteger(data, GetHandleId(GetExpiredTimer()), StringHash("MovementSpeedBuffLevel")));
            UnitAddAbility(LoadUnitHandle(data, GetHandleId(GetExpiredTimer()), StringHash("GalenUnit")), 'B04W');
            UnitAddAbility(LoadUnitHandle(data, GetHandleId(GetExpiredTimer()), StringHash("GalenUnit")), 'B04V');
            FlushChildHashtable(data, GetHandleId(GetExpiredTimer()));
            DestroyTimer(GetExpiredTimer());
          });
        }
        UnitAddAbility(damager, 'B04W');
        UnitAddAbility(damager, 'B04V');
      }
      //Fury handling
      if (GetUnitAbilityLevel(damager, furyBuff) > 0 && IsUnitEnemy(GetTriggerUnit(), GetOwningPlayer(damager))) {
        tempInteger[0] = LoadInteger(data, GetHandleId(damager), StringHash("AdditionalAgility"));
        if (tempInteger[0] < (20 + 10 * GetUnitAbilityLevel(damager, fury))) {
          tempInteger[1] = GetUnitAbilityLevel(damager, fury) + 2;
          if (tempInteger[1] + tempInteger[0] > (20 + 10 * GetUnitAbilityLevel(damager, fury))) {
            tempInteger[1] = (20 + 10 * GetUnitAbilityLevel(damager, fury)) - tempInteger[0];
          }
          tempInteger[0] += tempInteger[1];
          SaveInteger(data, GetHandleId(damager), StringHash("AdditionalAgility"), tempInteger[0]);
          tempTimer[0] = CreateTimer();
          SaveUnitHandle(data, GetHandleId(tempTimer[0]), StringHash("GalenUnit"), damager);
          SaveInteger(data, GetHandleId(tempTimer[0]), StringHash("AdditionalAgility"), tempInteger[1]);
          SetHeroAgi(damager, GetHeroAgi(damager, false) + tempInteger[1], true);
          TimerStart(tempTimer[0], 5, false, function(){
            SetHeroAgi(LoadUnitHandle(data, GetHandleId(GetExpiredTimer()), StringHash("GalenUnit")), 
            GetHeroAgi(LoadUnitHandle(data, GetHandleId(GetExpiredTimer()), StringHash("GalenUnit")), false) -
            LoadInteger(data, GetHandleId(GetExpiredTimer()), StringHash("AdditionalAgility")), true);
            SaveInteger(data, GetHandleId(LoadUnitHandle(data, GetHandleId(GetExpiredTimer()), StringHash("GalenUnit"))), StringHash("AdditionalAgility"),
            LoadInteger(data, GetHandleId(LoadUnitHandle(data, GetHandleId(GetExpiredTimer()), StringHash("GalenUnit"))), StringHash("AdditionalAgility"))
            - LoadInteger(data, GetHandleId(GetExpiredTimer()), StringHash("AdditionalAgility")));
            FlushChildHashtable(data, GetHandleId(GetExpiredTimer()));
            DestroyTimer(GetExpiredTimer());
          });
        }
      }

      tempEffect[0] = LoadEffectHandle(abilityData, GetHandleId(GetTriggerUnit()), StringHash("RejuvEffectYellow"));
      if (tempEffect[0] != null) {
        SaveBoolean(abilityData, GetHandleId(GetTriggerUnit()), StringHash("IsUnitRejuvEffect"), false);
        IssueImmediateOrder(GetTriggerUnit(), "stop");
      }
      tempEffect[0] = LoadEffectHandle(abilityData, GetHandleId(GetTriggerUnit()), StringHash("RejuvEffectGreen"));
      if (tempEffect[0] != null) {
        SaveBoolean(abilityData, GetHandleId(GetTriggerUnit()), StringHash("IsUnitRejuvEffect"), false);
        DestroyEffect(tempEffect[0]);
        RemoveSavedHandle(abilityData, GetHandleId(GetTriggerUnit()), StringHash("RejuvEffectGreen"));
        tempTimer[0] = LoadTimerHandle(abilityData, GetHandleId(GetTriggerUnit()), StringHash("RejuvTimer"));
        FlushChildHashtable(abilityData, GetHandleId(tempTimer[0]));
        DestroyTimer(tempTimer[0]);
        RemoveSavedHandle(abilityData, GetHandleId(GetTriggerUnit()), StringHash("RejuvTimer"));
      }

      if ((GetUnitAbilityLevel(GetTriggerUnit(), stormShield) > 0) &&
        ( !IsUnitType(damager, UNIT_TYPE_MAGIC_IMMUNE) && !IsUnitType(damager, UNIT_TYPE_MECHANICAL) ) ) {
        tempTimer[0] = LoadTimerHandle(abilityData, GetHandleId(GetTriggerUnit()), StringHash("StormShieldCooldownTimer"));
        if (TimerGetRemaining(tempTimer[0]) <= 0.1) {
          TimerStart(tempTimer[0], 30 - GetUnitAbilityLevel(GetTriggerUnit(), stormShield) * 6, false, null);
          UnitDamageTarget(GetTriggerUnit(), damager, 150, true, true, ATTACK_TYPE_MAGIC, DAMAGE_TYPE_NORMAL, WEAPON_TYPE_WHOKNOWS);
        }
      }

      if (GetUnitAbilityLevel(GetTriggerUnit(), 'A0FZ') > 0) {
        tempInteger[0] = 10 * GetUnitAbilityLevel(GetTriggerUnit(), 'A0FZ');
        SetUnitState(GetTriggerUnit(), UNIT_STATE_LIFE, GetUnitState(GetTriggerUnit(), UNIT_STATE_LIFE) + 
        ( GetEventDamage() * (1 - (tempInteger[0] / 100)) ));
      }

      damager = null;

      return false;
    }

    function addEventDamaged()  -> nothing {
      unit toWhichUnit = GetTriggerUnit();
      trigger onUnitDamaged;
      if ( GetUnitAbilityLevel(toWhichUnit, 'Aloc') == 0 && GetUnitAbilityLevel(toWhichUnit, 'Avul') == 0 ) {
        onUnitDamaged = CreateTrigger();
        SaveTriggerHandle(onDamagedTriggers, GetHandleId(toWhichUnit), StringHash("EventUnitDamaged"), onUnitDamaged);
        TriggerRegisterUnitEvent(onUnitDamaged, toWhichUnit, EVENT_UNIT_DAMAGED);
        TriggerAddCondition(onUnitDamaged, Filter(function eventDamagedHandler));
      }
      toWhichUnit = null;
      onUnitDamaged = null;
    }

    function removeEventDamaged()  -> nothing {
      unit toWhichUnit = GetTriggerUnit();
      trigger onUnitDamaged = LoadTriggerHandle(onDamagedTriggers, GetHandleId(toWhichUnit), StringHash("EventUnitDamaged"));
      FlushChildHashtable(onDamagedTriggers, GetHandleId(onUnitDamaged));
      TriggerClearConditions(onUnitDamaged);
      DestroyTrigger(onUnitDamaged);
      onUnitDamaged = null;
      toWhichUnit = null;
    }

    function init()  -> nothing {
      trigger onUnitSpawn = CreateTrigger();
      trigger onUnitDeath = CreateTrigger();
      region map = CreateRegion();
      group allUnits = CreateGroup();
      unit tempUnit;
      trigger tempTrigger;
      RegionAddRect(map, GetPlayableMapRect());
      TriggerRegisterEnterRegion(onUnitSpawn, map, null);
      TriggerAddAction(onUnitSpawn, function addEventDamaged);
      TriggerRegisterLeaveRegion(onUnitDeath, map, null);
      TriggerAddAction(onUnitDeath, function removeEventDamaged);
      GroupEnumUnitsInRect(allUnits, GetPlayableMapRect(), null);
      while (FirstOfGroup(allUnits) != null) {
        tempUnit = FirstOfGroup(allUnits);
        GroupRemoveUnit(allUnits, tempUnit);
        if ( GetUnitAbilityLevel(tempUnit, 'Aloc') == 0 && GetUnitAbilityLevel(tempUnit, 'Avul') == 0 ) {
          tempTrigger = CreateTrigger();
          SaveTriggerHandle(onDamagedTriggers, GetHandleId(tempUnit), StringHash("EventUnitDamaged"), tempTrigger);
          TriggerRegisterUnitEvent(tempTrigger, tempUnit, EVENT_UNIT_DAMAGED);
          TriggerAddCondition(tempTrigger, Filter(function eventDamagedHandler));
        }
      }
      DestroyGroup(allUnits);
      allUnits = null;
      tempUnit = null;
      map = null;
      tempTrigger = null;
    }

    function onInit()  -> nothing {
      trigger initTrigger = CreateTrigger();
      TriggerRegisterTimerEvent(initTrigger, 0.01, false);
      TriggerAddAction(initTrigger, function init);
      
    }
  }


//! endzinc